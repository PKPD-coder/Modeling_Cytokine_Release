METHOD stiff

STARTTIME = 0
STOPTIME=180
TOLERANCE = 1e-11

;--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
;model based on equations in original paper from Van De Vyver and colleagues			DOI: 10.1158/1535-7163.MCT-20-0269
;details and description of the various model components can be found therein
;parameters that differ from the reported values in the original model are denoted by &&
;changes in parameters (i.e. affinity and target expression) were made for the sake of simulations shown in the present manuscript
;--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

;SYNAPSE FORMATION
E = CD3totMolar - BE - TBE								;free CD3 									[nM]
T = RtotMolar - TB -TBE									;free target antigen 								[nM]
B' =  -konCD3*B*E + koffCD3*BE - konR*T*B + koffR*TB 					;free CD3-bispecific antibody							[nM]
BE' = konCD3*B*E - koffCD3*BE - konR*T*BE +koffR*TBE				;binary complex formation between CD3-bispecific antibody and CD3		[nM]
TB' = konR*T*B - koffR*TB - konCD3*TB*E + koffCD3*TBE				;binary complex formation between CD3-bispecific antibody and target antigen	[nM]
TBE' = konCD3*TB*E + konR*BE*T - koffR*TBE - koffCD3*TBE				;trimeric complex (i.e. immune synapse) formation				[nM]

TBEPC =  (TBE)*10^-6/(TA)								;average concentration of immune synapse per tumor cell				[nM]
TBEPCabs = TBEPC*6.022*10^23/10^9							;average number of immune synapses per tumor cell				[#/cell]


;CD25 EXPRESSION INDUCTION
delta=densityR/(densityRref)								;target antigen expression ratio that will affect extent of T-cell activation		[N/A]
densityRref = 230000									;target antigen from the reference system used for model building			[#/cell]
STIM =((Emax*delta)*TBEPCabs^g)/(Complex50^g+TBEPCabs^g)				;Immune stimulatory effect of immune synapses					[N/A]
ktr = 3/tau										;transit rate of the delayed effect on T cell activation				[1/h]				
k1'=ktr*(STIM-k1)									;transit compartment 1; forming the delay in T cell activation			[N/A]
k2'= ktr*(k1-k2)										;transit compartment 2								[N/A]
k3'=ktr*(k2-k3)										;transit compartment 3								[N/A]

CD25' = kin*(1+k3)*((reg0)/(reg)) - kreg*(CD25);						;change in concentration of activated (CD8+CD25+) T cells			[cells/uL]
kreg =kin/CD25base									;removal rate of activated T cells							[1/h]
transit1' = kreg*(CD25-CD25base) - kreg*transit1						;transit compartment 1; gradual formation of negative feedback on T cell activation	[cells/uL]
transit2' = kreg*transit1 - kreg*transit2							;transit compartment 2								[cells/uL]
transit3' = kreg*transit2 - kreg*transit3							;transit compartment 3								[cells/uL]
reg' = kreg*transit3 - kreg*(reg-reg0)							;Negative feedback term								[cells/uL]

;TUMOR
kkill = kappa*kg*(CD25^h/(CD25^h+CD2550^h))						;tumor kill rate in function of T cell activation					[1/h]
TA' = kg*TA*(1-TA/K) - kkill*TA								;tumor growth and killing								[cells/uL]


;PARAMETERS
				
CD3tot = Tcell*densityCD3*10^6			;CD3 concentration		[1/L]
CD3totMolar = 10^9*CD3tot/(6.022*10^23)	;CD3 concentration		[nM]
Rtot = TA*densityR*10^6				;antigen concentration		[1/L]
RtotMolar = 10^9*Rtot/(6.022*10^23)		;antigen concentration		[nM]

konCD3 = 0.036					;association rate CD3		[1/(h*nM)]
koffCD3 = 3.6					;dissociation rate CD3		[1/h]
konR = 0.05					;association rate antigen		[1/(h*nM)]
						;&&association constant was varied to study effect on T cell activation: 0.05 (low) and 0.2 (high) affinity
koffR = 3.6					;dissociation rate antigen	[1/h]

densityCD3 = 100000				;CD3 density 			[1/cell]
densityR = 200000				;tumor antigen density 		[1/cell]   			
						;&&antigen density was varied to study its effect on T cell activation: 10'000 (low), 50'000 (mid), 200'000 (high) antigen/cell

ConcMolar = 20					;Drug concentration		[nM]
reg0 = CD25base				;baseline feedback component 	[cells/uL]
CD25base = 3					;baseline CD25pos cells 	[cells/uL]
iniTumor = 6					;initial tumour concentration	[cells/uL]
Tcell = 230					;Tcell concentration		[cells/uL]

Emax = 178					;maximal T cell activation					[N/A]
Complex50 = 0.49				;concentration of synapses for half-maximal T cell activation 	[1/cell]
g=1						;hill coefficient on T cell activation steepness			[N/A]
tau = 75					;transit time for T cell activation					[h]
kin = 0.0598					;baseline input rate of activated T cells 				[1/h]
kg = 0.058					;tumor growth rate						[1/h]
K = 326						;tumor carrying capacity						[cells/uL]
kappa = 1					;proportionality constant of max kill rate with respect to kg		[N/A]
CD2550 = 7.8					;level of T cell activation leading to half-maximal killing		[cells/uL]
h = 3.1						;hill coefficient on tumor cell killing steepness			[N/A]

;INITIAL VALUES
INIT BE =0
INIT TBE =0
INIT TA = iniTumor
INIT TB =0
INIT B =  concmolar
INIT k1=0
INIT k2 =0
INIT k3=0
INIT reg =reg0
INIT transit1=0
INIT transit2=0
INIT transit3=0
INIT CD25= CD25base

